apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'com.mapbox.android.sdk.versions'
apply from: "${rootDir}/gradle/ktlint.gradle"
apply from: file("${rootDir}/gradle/artifact-settings.gradle")

dokka {
    outputDirectory = "$buildDir/kdoc"
    configuration {
        moduleName = 'libnavigation-core'
        reportUndocumented = true

        perPackageOption {
            prefix = "com.mapbox.navigation.core.internal"
            suppress = true
        }
    }
}

android {
    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildToolsVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro', "${rootDir}/proguard/proguard-project.pro"

        buildConfigField 'String', "MAPBOX_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.ext.versionName)
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

configurations.all
        {
            resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'androidx.lifecycle') {
                        details.useVersion "2.2.0"
                }
                if (details.requested.group == 'androidx.collection'
                        && details.requested.name.contains('collection')) {
                    details.useVersion "1.1.0"
                }
                if (details.requested.group == 'org.jetbrains'
                        && details.requested.name.contains('annotations')) {
                    details.useVersion "16.0.1"
                }
                if (details.requested.group == 'androidx.arch.core'
                        && details.requested.name.contains('core-runtime')) {
                    details.useVersion "2.1.0"
                }
            }
        }

dependencies {
    implementation dependenciesList.mapboxAnnotations
    api dependenciesList.mapboxAndroidCommon

    api project(':libnavigation-base')
    implementation project(':libnavigation-util')
    implementation project(':libnavigator')
    runtimeOnly project(':libdirections-hybrid')
    runtimeOnly project(':libtrip-notification')
    runtimeOnly dependenciesList.mapboxLogger
    implementation project(':libnavigation-metrics')
    implementation dependenciesList.mapboxAndroidAccounts
    implementation dependenciesList.mapboxSdkTurf

    //ktlint
    ktlint dependenciesList.ktlint

    implementation dependenciesList.kotlinStdLib
    implementation dependenciesList.coroutinesAndroid

    testImplementation project(':libtesting-utils')
    apply from: "${rootDir}/gradle/unit-testing-dependencies.gradle"
    testImplementation dependenciesList.commonsIO

    testImplementation project(':libdirections-hybrid')
    testImplementation project(':libtrip-notification')

    // android test
    androidTestImplementation dependenciesList.androidXAppCompat
    androidTestImplementation project(':libtesting-ui')
    androidTestImplementation dependenciesList.androidXConstraintLayout
    androidTestImplementation dependenciesList.mapboxMapSdk
    androidTestImplementation dependenciesList.mapboxLogger
    androidTestImplementation project(':libtrip-notification')
    androidTestImplementation project(':libnavigation-ui')
    androidTestImplementation dependenciesList.timber
}

apply from: "${rootDir}/gradle/track-public-apis.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/dependencies-graph.gradle"
apply from: "${rootDir}/gradle/bintray-publish.gradle"
apply from: "${rootDir}/gradle/download-schema.gradle"
